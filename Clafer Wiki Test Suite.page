This test suite demonstrates how an example model can be processed.

# Example model
Telematics Example (from [](http://gsd.uwaterloo.ca/node/291)):

Generic PLA

``` {.clafer}
abstract options
    xor size
        small
        large
    cache ?
        size : integer
            fixed ?
    [small && cache => fixed]

abstract comp
    version : integer = 1 + 2

abstract ECU : comp

abstract display : comp
    server -> ECU
    `options
    [this.version >= server.version]
```

Specific PLA (template)

```{.clafer}
abstract plaECU : ECU
    `display 1..2
        [server = parent
         !options.cache]

ECU1 : plaECU

ECU2 : plaECU ?
    master -> ECU1
```

Feature Model for the specific PL

```{.clafer}
telematicsSystem
    xor channel
        single
        dual

    extraDisplay ?

    xor size
        small
        large

    [dual <=> ECU2
     extraDisplay <=> #ECU1.display = 2
     extraDisplay <=> (ECU2 => #ECU2.display = 2)
     large <=> !plaECU.display.options.size.small
     small <=> !plaECU.display.options.size.large]
```

Concrete Product

```{.clafer}
[dual
 extraDisplay
 telematicsSystem.size.large]
```

This example's output in HTML can be found in claferwiki/static/clafer/